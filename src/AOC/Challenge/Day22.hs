{-# OPTIONS_GHC -Wno-unused-imports   #-}
{-# OPTIONS_GHC -Wno-unused-top-binds #-}

-- |
-- Module      : AOC.Challenge.Day22
-- License     : BSD3
--
-- Stability   : experimental
-- Portability : non-portable
--
-- Day 22.  See "AOC.Solver" for the types used in this module!
--
-- After completing the challenge, it is recommended to:
--
-- *   Replace "AOC.Prelude" imports to specific modules (with explicit
--     imports) for readability.
-- *   Remove the @-Wno-unused-imports@ and @-Wno-unused-top-binds@
--     pragmas.
-- *   Replace the partial type signatures underscores in the solution
--     types @_ :~> _@ with the actual types of inputs and outputs of the
--     solution.  You can delete the type signatures completely and GHC
--     will recommend what should go in place of the underscores.

module AOC.Challenge.Day22 (
    day22a
  , day22b
  ) where

import           AOC.Prelude
import Linear
import Data.Ix

import qualified Data.Graph.Inductive           as G
import qualified Data.IntMap                    as IM
import qualified Data.IntSet                    as IS
import qualified Data.List.NonEmpty             as NE
import qualified Data.List.PointedList          as PL
import qualified Data.List.PointedList.Circular as PLC
import qualified Data.Map                       as M
import qualified Data.OrdPSQ                    as PSQ
import qualified Data.Sequence                  as Seq
import qualified Data.Set                       as S
import qualified Data.Set.NonEmpty              as NES
import qualified Data.Text                      as T
import qualified Data.Vector                    as V
import qualified Linear                         as L
import qualified Text.Megaparsec                as P
import qualified Text.Megaparsec.Char           as P
import qualified Text.Megaparsec.Char.Lexer     as PP

data Land = Wall | Grass deriving (Eq, Ord)

type Side = (Point, Point)

data Instruction = Move Int | Rotate Dir deriving (Show, Eq)

charMap :: Char -> Maybe Land
charMap '#' = Just Wall
charMap '.' = Just Grass
charMap _   = Nothing

parsePath = P.many ((Move <$> pDecimal) <|> (Rotate <$> (West <$ P.char 'L' <|> East <$ P.char 'R')))

password :: Point -> Dir -> Int
password (V2 x y) = ((1000 * (y+1)) + (4 * (x+1)) +) . \case
    East -> 0
    South -> 1
    West -> 2
    North -> 3

getRows, getCols :: Map Point Land -> Map Int (Map Point Land)
getRows = M.mapKeysWith M.union (\(V2 _ r) -> r) . M.mapWithKey M.singleton
getCols = M.mapKeysWith M.union (\(V2 c _) -> c) . M.mapWithKey M.singleton

getTiles :: Map Int (Map Point Land) -> Map Int (Map Point Land) -> Int -> Point -> Dir -> [Point]
getTiles rows cols n loc@(V2 c r) = \case
    North -> getPath . M.toDescList $ (cols M.! c)
    East  -> getPath . M.toAscList $ (rows M.! r)
    South -> getPath . M.toAscList $ (cols M.! c)
    West  -> getPath . M.toDescList $ (rows M.! r)
    where
        getPath = map fst . takeWhile ((/=Wall).snd) . take (n+1) . dropWhile ((/=loc).fst) . cycle

findPassword (jungle, path) = do
    let startHeading = East
    (start, _) <- M.lookupMin . M.filterWithKey (\(V2 _ y) a -> Grass == a && y == 0) $ jungle
    let (end, endHeading) = foldl' go (start, startHeading) path
    return $  password end endHeading
        where
            rows = getRows jungle
            cols = getCols jungle
            findEndPoint n loc h = last $ getTiles rows cols n loc h
            go :: (Point, Dir) -> Instruction -> (Point, Dir)
            go (loc, h) (Rotate d) = (loc, h<>d)
            go (loc, h) (Move n) = (findEndPoint n loc h, h)

findCubicPassword (jungle, path) = do
    let startHeading = East
    (start, _) <- M.lookupMin . M.filterWithKey (\(V2 _ y) a -> Grass == a && y == 0) $ jungle
    let (end, endHeading) = foldl' go (start, startHeading) path
    return $  password end endHeading
        where
            findNextPoint = wrapSide jungle
            go :: (Point, Dir) -> Instruction -> (Point, Dir)
            go (loc, h) (Rotate d) = (loc, h<>d)
            go (loc, h) (Move 0) = (loc, h)
            go (loc, h) (Move n) = let next@(locN, hN) = findNextPoint loc h
                                    in case jungle M.! locN of
                                         Wall -> (loc, h)
                                         Grass -> go next (Move (n-1))

-- Sad to hard code mappings, but the sides were autogenerated at least.
side1, side2, side3, side4, side5, side6, side7, side8, side9, side10, side11, side12, side13, side14 :: Side
side1  = (V2 50 0  , V2 99 0  )
side2  = (V2 100 0 , V2 149 0 )
side3  = (V2 149 0 , V2 149 49)
side4  = (V2 100 49, V2 149 49)
side5  = (V2 99 50 , V2 99 99 )
side6  = (V2 99 100, V2 99 149)
side7  = (V2 50 149, V2 99 149)
side8  = (V2 49 150, V2 49 199)
side9  = (V2 0 199 , V2 49 199)
side10 = (V2 0 150 , V2 0 199 )
side11 = (V2 0 100 , V2 0 149 )
side12 = (V2 0 100 , V2 49 100)
side13 = (V2 50 50 , V2 50 99 )
side14 = (V2 50 0  , V2 50 49 )

sides :: Map (Dir, Side) (Dir, Side, Bool)
sides = M.fromList
    [ ((North,  side1) , (East,  side10, True ))
    , ((North,  side2) , (North, side9 , True ))
    , ((East,   side3) , (West,  side6 , False))
    , ((South,  side4) , (West,  side5 , True ))
    , ((East,   side5) , (North, side4 , True ))
    , ((East,   side6) , (West,  side3 , False))
    , ((South,  side7) , (West,  side8 , True ))
    , ((East,   side8) , (North, side7 , True ))
    , ((South,  side9) , (South, side2 , True ))
    , ((West,  side10) , (South, side1 , True ))
    , ((West,  side11) , (East,  side14, False))
    , ((North, side12) , (East,  side13, True ))
    , ((West,  side13) , (South, side12, True ))
    , ((West,  side14) , (East,  side11, False))
    ]

wrapSide :: Map Point Land -> Point -> Dir -> (Point, Dir)
wrapSide jungle p d
  | (p + dirVec d) `M.member` jungle = (p + dirVec d, d)
  | otherwise = ((range s') !! idx, d')
    where
        ((_, s), (d', s', normal)) = head . M.toList . M.filterWithKey (\(d', s) _ -> d == d' && inRange s p) $ sides
        idx
          | normal = index s p
          | otherwise = rangeSize s' - 1 - (index s p)

day22a :: (Map Point Land, [Instruction]) :~> Int
day22a = MkSol
    { sParse = sequence . bimap (parseAsciiMap charMap) (parseMaybeLenient parsePath) <=< splitInput
    , sShow  = show
    , sSolve = findPassword
    }

day22b :: (Map Point Land, [Instruction]) :~> Int
day22b = MkSol
    { sParse = sequence . bimap (parseAsciiMap charMap) (parseMaybeLenient parsePath) <=< splitInput
    , sShow  = show
    , sSolve = findCubicPassword
    }

splitInput :: String -> Maybe (String, String)
splitInput s = do
    let [mp, path] = splitOn "\n\n" s
    return (mp, path)
